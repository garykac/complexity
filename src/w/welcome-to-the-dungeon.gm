NAME: Welcome to the Dungeon

SECTION: Assumptions

IMPORT: Player, ChooseFirstPlayer, Pass, NextPlayerClockwise
IMPORT: Card, Deck

SECTION: Vocabulary

MonsterCard: Card
Strength: Attribute of MonsterCard
Weakness: Attribute of MonsterCard
	// Icon showing EquipmentTiles that can defeat this MonsterCard

AdventurerTile: Noun
	// 4 in game, but only using the Warrior
BaseHP: Attribute of AdventurerTile
	// Base hit points

EquipmentTile: Noun
	// 24 total. 6 per AdventurerTile.
BonusHP: Attribute of EquipmentTile
	Optional hit point bonus provided by the equipment

SuccessCard: Card
	Used to track successful runs through the dungeon

PlayerAid: Noun
	This is also used to track Failures. White background (0 failures) on one side; red (1 failure) on other.

MonsterDeck: Deck
	// Deck of MonsterCards

Inventory: Noun
	Collection of EquipmentTiles
DungeonPile: Noun
	Collection of MonsterCards
HitPoints: Noun
	Player's current health tracker

SECTION: Setup

Setup: Verb
	SetupAdventurer
	SetupMonsterDeck
	SetupPlayerAid
	Leave space on table for the DungeonPile
	ChooseFirstPlayer

SetupAdventurer: Verb
	// For first game, choose the "Warrior" as the Adventurer
	Place AdventurerTile on table
	Place the 6 matching EquipmentTiles in the Inventory (next to AdventurerTile)
	// Note: Matching EquipmentTiles are the same color as Adventurer

SetupMonsterDeck: Verb
	Shuffle all the MonsterCards
	Place facedown to create MonsterDeck

SetupPlayerAid: Verb
	For each Player:
		Place PlayerAid in front of you with the white background (=0 failures) side showing

SECTION: Gameplay

PlayGame: Verb
	Repeat until Winner:
		BiddingPhase
		DungeonPhase
					
BiddingPhase: Verb
	Repeat until all but one Player has chosen Pass:
		Choose one:
			DrawCard
			Pass
		NextPlayerClockwise
	! Once you Pass, you are done until after the DungeonPhase.

DungeonPhase: Verb
	The player who did not Pass does the following:
		CalculateHitPoints
		EnterDungeon
		If HitPoints >= 0:
			Take a SuccessCard
		Otherwise:
			Failure

DrawCard: Verb
	Draw from the MonsterDeck
	Choose one:
		AddMonster
		RemoveMonster

AddMonster: Verb
	Add the MonsterCard face down on the DungeonPile
	
RemoveMonster: Verb
	Place the MonsterCard in front of you, face-down
	Remove an EquipmentTile from the Inventory and place it on the MonsterCard
	! You may not remove a MonsterCard if the Inventory is empty

CalculateHitPoints: Verb
	HitPoints = BaseHP from the AdventurerTile
	Add the BonusHP from all EquipmentTiles that remain in the Inventory

EnterDungeon: Verb
	Repeat until the DungeonPile is empty:
		Reveal top card of DungeonPile
		If an EquipmentTile in the Inventory has an icon that matches the MonsterCard's Weakness:
			Discard the MonsterCard
		Otherwise:
			Deduct MonsterCard's Strength from your HitPoints

Failure: Verb
	If PlayerAid is showing the white background:
		Flip it to show the red background
	Otherwise:
		You are eliminated from the game

SECTION: Endgame Scoring

Winner: Verb
	The winner is the first Player with 2 SuccessCards or the last remaining Player

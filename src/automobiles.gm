NAME: Automobiles
// Year: 2016
// Designer: David Short
// Players: 2-5
// Time: 45m

SECTION: Assumptions

IMPORT: Player, Box
IMPORT: NextPlayerClockwise
IMPORT: Gameboard, Token, ChoosePlayerComponents
IMPORT: Card

SECTION: Vocabulary

TrackBoard: Gameboard

Space: Part of TrackBoard
Segment: Part of Space
	Optional subdivision of a Space that allows multiple RaceCars to occupy the same Space

WhiteSpace: Space
LightGraySpace: Space
DarkGraySpace: Space
BlackSpace: Space

AdjacentSpace: Space
	Any Space that is touching the current Space, even if only a single point (so diagonal Spaces are adjacent).

StartPosition: WhiteSpace
Position: Attribute of StartPosition
	// Starting position value: P1, P2, ... P5
StartingMoney: Attribute of StartPosition
	// Amount of money at start of game for the Player starting in this position.

FinishLine: Part of TrackBoard

LapBoard: Gameboard
LapsRemaining: Attribute of LapBoard

PlayerBoard: Gameboard
ActivePile: Attribute of PlayerBoard
UsedPile: Attribute of PlayerBoard
DiscardPile: Attribute of PlayerBoard

Cube: Noun

Stock: Noun
	Set of all available Cubes
	
DrawBag: Noun

AutomobileCard: Card
	//Type: "Engine", "Garage", "Handling", "Performance", "Pit"
	//Color: Matches corresponding Cube
Cost: Attribute of AutomobileCard
	Cost to purchase the Cube associated with this card.
Value: Attribute of AutomobileCard
	Amount of money the matching Cube is worth during the BuyPhase

WearCard: Noun
WearCube: Cube	// Brown

GearCard: Card
Cost: Attribute of GearCard
Value: Attribute of GearCard

GearCube: Cube

Gear3Cube: GearCube	// White
Gear4Cube: GearCube	// Light Gray
Gear5Cube: GearCube	// Dark Gray
Gear6Cube: Cube	// Black

EngineCube: Cube	// Blue
GarageCube: Cube	// Yellow
HandlingCube: Cube	// Red
PerformanceCube: Cube	// Green
PitCube: Cube	// Purple

StartPlayerCard: Card
StartPlayer: Player
	The Player with the StartPlayerCard

LapMarker: Token
RaceCar: Token

CurrentPosition: Noun
	Position of your RaceCar at the beginning of your ActionPhase
	! InnerLanesLeadOuterLanes

InnerLanesLeadOuterLanes: Constraint
	Inner lanes lead outer lanes in case of a tie

SECTION: Setup

Setup: Verb
	Select which side of the TrackBoard you wish to use
	Place Stock (with all the Cubes) next to TrackBoard
	Place GearCards and WearCard next to matching section by the Stock
	SetupAutomobileCards
	Give StartPlayerCard to a random Player
	For each Player, starting with the StartPlayer:
		ChoosePlayerComponents
			* RaceCar
			* PlayerBoard
			* LapMarker
		Place RaceCar on the empty StartPosition with the lowest Position value
		Place LapMarker on the LapBoard where LapsRemaining is 3
		Take a DrawBag and 12 starting Cubes:
			* 2 Gear4Cubes (light gray)
			* 5 Gear3Cubes (white)
			* 5 GarageCubes (yellow)
		BuyStartCubes
		Put all Cubes in the DrawBag
		DrawCube x7

SetupAutomobileCards: Verb
	// Normally, draw 1 card at random for each type.
	// These are the cards recommended for the first game.
	Select the AutomobileCards named: "Manager", "Crew Chief", "Suspension", "Gearbox", "Hybrid Engine"
	Place them face up next to their matching color by the Stock
	Return remaining AutomobileCards to the Box

BuyStartCubes: Verb
	Using your StartingMoney (based on StartPosition):
		Buy as many Cubes as you wish by paying the Costs on the corresponding Card
	! UnspentMoneyIsLost

UnspentMoneyIsLost: Constraint
	Any unspent money is lost

SECTION: Gameplay

PlayGame: Verb
	Repeat until a LapMarker is on "Finish":
		GameRound

GameRound: Verb
	For each Player, starting with the StartPlayer
		Choose one:
			StandardTurn
			AlternativeTurn
		NextPlayerClockwise
	// So that each Player has the same number of turns

StandardTurn: Verb
	ActionPhase
	BuyPhase
	CarPhase
	DeclinePhase
	EndPhase

ActionPhase: Verb
	Repeat as many times as you wish:
		Move a Cube from your ActivePile to your UsedPile
		Apply effect based on the Cube type:
			WearCube: WearAction
			Gear3Cube: GearAction<WhiteSpace>
			Gear4Cube: GearAction<LightGraySpace>
			Gear5Cube: GearAction<DarkGraySpace>
			Gear6Cube: GearAction<BlackSpace>
			EngineCube: EngineAction
			GarageCube: GarageAction
			HandlingCube: HandlingAction
			PerformanceCube: PerformanceAction
			PitCube: PitAction
		! MustPerformAllCardEffects

MustPerformAllCardEffects: Constraint
	When applying an AutomobileCard's effect:
		If some of the effects cannot be applied:
			You may not apply any of the effects
	! For WearCubes, you can still apply the other effects even if the Stock is out of WearCubes

MoveRaceCar: Verb
	Starting from your RaceCar or one of the Cubes you placed on a Space this turn:
		Place the Cube that you used to make the move onto an AdjacentSpace
	! You may not move into a Space that has an opponent's RaceCar
		Unless the Space has multiple Segments and there is an empty Segment
	! When moving into a Space with multiple Segments:
		Move your RaceCar forward to the frontmost empty Segment
	! Any Cube you place on a Space is still considered to be in your UsedPile

	// Placing a Cube instead of moving your RaceCar directly helps you track your actions.

WearAction: Verb
	Move the WearCube from your UsedPile into your DiscardPile

GearAction<Space>: Verb
	MoveRaceCar 1 Space that matches the color

EngineAction: Verb
	// From the "Hybrid Engine" card
	GainWear
	If you have more EngineCube (blue) on your PlayerBoard than any other Player's DiscardPile:
		You may MoveRaceCar up to 2 Spaces of the same type (color)

GarageAction: Verb
	// From the "Manager" card
	You may RemoveCube 1 Cube from your ActivePile
	You may move 1 Cube from your ActivePile or DiscardPile into your DrawBag

HandlingAction: Verb
	// From the "Suspension" card
	GainWear
	MoveRaceCar 1 Space matching a GearCube in your DiscardPile

PerformanceAction: Verb
	// From the "Gearbox" card
	GainWear
	MoveRaceCar up to as many LightGraySpaces as your CurrentPosition
	If you are in last position:
		You may move 1 additional LightGraySpace

PitAction: Verb
	// From the "Crew Chief" card
	Repeat until you draw a non-WearCube:
		DrawCube
	RemoveCube all WearCubes from your DiscardPile

BuyPhase: Verb
	For each Cube in your ActivePile:
		Add the Value from the matching AutomobileCard to your money
	Buy as many Cubes are you wish with your money
	Place purchased Cubes into your UsedPile
	! UnspentMoneyIsLost

CarPhase: Verb
	Move your RaceCar up to the furthest Cube that you placed on the TrackBoard
	If you pass the FinishLine:
		Move your LapMarker down one space on the LapBoard

DeclinePhase: Verb
	If your RaceCar is in a Space or Segment immediately behind an opponent's RaceCar:
		// You are Drafting and take no wear.
		Exit
	Based on darkest color Space that you moved into this turn:
		WhiteSpace: GainWear
		LightGraySpace: GainWear x2
		DarkGraySpace: GainWear x3
		BlackSpace: GainWear x4
	! For Spaces with multiple colors, treat it as if it was the darker color
	! Don't gain any WearCubes if you didn't move this turn

EndPhase: Verb
	Move all of your Cube from the TrackBoard, ActivePile and UsedPile into your DiscardPile
	DrawCube x7

AlternativeTurn: Verb
	RemoveCube all WearCubes from your ActivePile
	EndPhase

DrawCube: Verb
	Draw a Cube from your DrawBag and place it in your ActivePile
	! If your DrawBag is empty:
		Place all Cubes from your DiscardPile into your DrawBag

RemoveCube: Verb
	Move the Cube into the Stock
	
GainWear: Verb
	If there is a WearCube available in the Stock:
		Take a WearCube from the Stock and put it in your UsedPile

SECTION: Endgame Scoring

DetermineWinner: Verb
	The winner is the RaceCar that went the furthest past the FinishLine
	! InnerLanesLeadOuterLanes
